plugins {
    id "io.micronaut.internal.starter.published-module"
    id "io.micronaut.internal.starter.convention"
    id "io.micronaut.starter.rocker"
    id "io.micronaut.internal.starter.coordinates"
}

sourceSets {
    main {
        rocker {
            srcDir("src/main/java")
        }
    }
}

dependencies {
    api platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    api "com.fizzed:rocker-runtime:$rockerVersion"
    api "io.micronaut:micronaut-inject"
    api "io.micronaut:micronaut-http"
    api "io.micronaut:micronaut-http-client"
    api("com.fasterxml.jackson.core:jackson-annotations")
    api "io.github.java-diff-utils:java-diff-utils:4.10"
    api "org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r"
    implementation("org.yaml:snakeyaml")
    implementation 'com.typesafe:config:1.4.2'
    implementation 'io.micronaut.testresources:micronaut-test-resources-build-tools:2.0.0-M1'
    implementation 'org.apache.commons:commons-compress:1.21'
    testImplementation("org.apache.groovy:groovy-xml")
}

def micronautVersionInfo = tasks.register("micronautVersionInfo", io.micronaut.internal.starter.tasks.WriteMicronautVersionInfoTask) {
    version = micronautVersion
    extraBomProperties = [
            'io.micronaut:micronaut-core',
            'io.micronaut.logging:micronaut-logging',
            'io.micronaut.grpc:micronaut-grpc',
            'io.micronaut.picocli:micronaut-picocli',
    ]
    outputDirectory = layout.buildDirectory.dir("version-info")
}

sourceSets.main.resources.srcDir(micronautVersionInfo)

rocker {
    javaVersion = '17'
    postProcessing = ['io.micronaut.starter.rocker.WhitespaceProcessor']
}

