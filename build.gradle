buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "io.spring.nohttp:nohttp-gradle:0.0.5.RELEASE"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.4.0"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "io.micronaut.docs:micronaut-docs-asciidoc-extensions:$micronautDocsVersion"
        classpath "io.micronaut.docs:micronaut-docs-gradle-plugins:$micronautDocsVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:2.1.0"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "5.1.0" apply false
}

repositories {
    mavenCentral()
}

version project.projectVersion

subprojects { Project subproject ->
    repositories {
        jcenter()
        maven { url "https://oss.jfrog.org/oss-snapshot-local" }
    }

    version project.projectVersion
    group "io.micronaut.starter"
    ext {
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    apply plugin:"groovy"
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply from: rootProject.file('gradle/license.gradle')
    apply plugin: "com.adarshr.test-logger"

    if (subproject.hasProperty("publish") && subproject.publish) {
        apply plugin:"java-library"
        apply from: "https://raw.githubusercontent.com/micronaut-projects/micronaut-build/v${micronautBuildVersion}/publishing.gradle"
        bintray.publish = true

        jar {
            manifest {
                attributes('Automatic-Module-Name': "${subproject.group}.${subproject.name}".replaceAll('[^\\w\\.\\$_]', "_"))
                attributes('Implementation-Version': projectVersion)
                attributes('Implementation-Title': title)
            }
        }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'


    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (project.name.startsWith("test-")) {
                    // Gradle TestKit needs Groovy 2.5
                    if (details.requested.group == "org.codehaus.groovy") {
                        details.useVersion("2.5.11")
                    }
                    if (details.requested.name == "spock-core") {
                        details.useVersion("1.3-groovy-2.5")
                    }
                } else {
                    //multiple actions can be specified
                    if (details.requested.group == "org.codehaus.groovy") {
                        details.useVersion(groovyVersion)
                    }
                    if (details.requested.name == "spock-core") {
                        details.useVersion(spockVersion)
                    }
                }
            }
        }
    }

    dependencies {
        if (project.name.startsWith("test-")) {
            implementation gradleTestKit()
        } else {
            annotationProcessor enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
            implementation enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
            annotationProcessor "io.micronaut:micronaut-inject-java"
            testAnnotationProcessor enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion")
            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        }
        testImplementation("org.codehaus.groovy:groovy:$groovyVersion")
        testImplementation("org.spockframework:spock-core:$spockVersion") {
            exclude group: "org.codehaus.groovy", module: "groovy-all"
        }
    }

    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/groovy/test')
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs.add('-parameters')
    }

    apply from: "${rootProject.projectDir}/gradle/checkstyle.gradle"
    if (!project.name.startsWith("test-")) {
        apply from: "${rootProject.projectDir}/gradle/test.gradle"
    }

}

apply from:"https://raw.githubusercontent.com/micronaut-projects/micronaut-docs/v${micronautDocsVersion}/gradle/docs.gradle"

Project starterCli = project('micronaut-cli')

starterCli.afterEvaluate {
    task generateManpageAsciiDoc(type: JavaExec) {
        dependsOn(starterCli.tasks.getByName('classes'))
        group = "Documentation"
        description = "Generate AsciiDoc manpage"
        classpath(starterCli.configurations.compile, starterCli.configurations.getByName('generateConfig'), starterCli.sourceSets.main.runtimeClasspath)
        main 'picocli.codegen.docgen.manpage.ManPageGenerator'
        args starterCli.mainClassName, "--outdir=${project.buildDir}/generated-picocli-docs", "-v", "--factory=io.micronaut.starter.cli.MicronautFactory" //, "--template-dir=src/docs/mantemplates"
    }

    task copyManPages(type: Copy) {
        dependsOn(generateManpageAsciiDoc)
        group = "Documentation"
        from file("${project.buildDir}/generated-picocli-docs")
        include "**/*.adoc"
        into file("${rootProject.buildDir.absolutePath}/generated/")
        filter { String line ->
            if (line == ':doctype: manpage' || line.startsWith('= ')) {
                return ""
            } else if (line.startsWith('==')) {
                return '=' + line
            } else {
                return line
            }
        }
    }

    publishGuide.dependsOn(copyManPages)
}