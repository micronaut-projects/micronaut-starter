buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
        classpath 'io.micronaut.build.internal:micronaut-gradle-plugins:4.0.0.RC2'
    }
}

repositories {
    mavenCentral()
}

version project.projectVersion

subprojects { Project subproject ->
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }

    version project.projectVersion
    group "io.micronaut.starter"
    ext {
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    apply plugin: "io.micronaut.build.internal.common"

    spotless {
      java {
        target 'src/main/java/**/*.java'
      }
    }

    if (subproject.hasProperty("publish") && subproject.publish) {

        apply plugin: "io.micronaut.build.internal.publishing"

        jar {
            manifest {
                attributes('Automatic-Module-Name': "${subproject.group}.${subproject.name}".replaceAll('[^\\w\\.\\$_]', "_"))
                attributes('Implementation-Version': projectVersion)
                attributes('Implementation-Title': title)
            }
        }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (project.name.startsWith("test-")) {
                    if (details.requested.group == "org.codehaus.groovy") {
                        details.useVersion(groovyVersion)
                    }
                    if (details.requested.name == "spock-core") {
                        details.useVersion(spockVersion)
                    }
                } else {
                    //multiple actions can be specified
                    if (details.requested.group == "org.codehaus.groovy") {
                        details.useVersion(groovyVersion)
                    }
                    if (details.requested.name == "spock-core") {
                        details.useVersion(spockVersion)
                    }
                }
            }
        }
    }

    dependencies {
        if (project.name.startsWith("test-")) {
            implementation gradleTestKit()
        } else {
            annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
            implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
            annotationProcessor "io.micronaut:micronaut-inject-java"
            testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
            testAnnotationProcessor "io.micronaut:micronaut-inject-java"
        }
        testImplementation("org.codehaus.groovy:groovy:$groovyVersion")
        testImplementation("org.spockframework:spock-core:$spockVersion") {
            exclude group: "org.codehaus.groovy", module: "groovy-all"
        }
    }

    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/groovy/test')
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs.add('-parameters')
    }

    apply from: "${rootProject.projectDir}/gradle/checkstyle.gradle"
    if (!project.name.startsWith("test-")) {
        apply from: "${rootProject.projectDir}/gradle/test.gradle"
    }

}

apply plugin: "io.micronaut.build.internal.docs"

Project starterCli = project('micronaut-cli')

starterCli.afterEvaluate {
    task generateManpageAsciiDoc(type: JavaExec) {
        dependsOn(starterCli.tasks.getByName('classes'))
        group = "Documentation"
        description = "Generate AsciiDoc manpage"
        classpath(starterCli.configurations.getByName('generateConfig'), starterCli.sourceSets.main.runtimeClasspath)
        main 'picocli.codegen.docgen.manpage.ManPageGenerator'
        args starterCli.mainClassName, "--outdir=${project.buildDir}/generated-picocli-docs", "-v", "--factory=io.micronaut.starter.cli.MicronautFactory" //, "--template-dir=src/docs/mantemplates"
    }

    task copyManPages(type: Copy) {
        dependsOn(generateManpageAsciiDoc)
        group = "Documentation"
        from file("${project.buildDir}/generated-picocli-docs")
        include "**/*.adoc"
        into file("${rootProject.buildDir.absolutePath}/generated/")
        filter { String line ->
            if (line == ':doctype: manpage' || line.startsWith('= ')) {
                return ""
            } else if (line.startsWith('==')) {
                return '=' + line
            } else {
                return line
            }
        }
    }

    publishGuide.dependsOn(copyManPages)
}
