plugins {
    id "io.micronaut.build.internal.common"
}

repositories {
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
}

////////////////////////////////////////////////////////////////////////
// TODO: Remove this once we have platform and core on the same release
ext {
    micronautPlatformVersion = "4.0.0-M1"
}
configurations.all {
    resolutionStrategy {
        force "io.micronaut.platform:micronaut-platform:${micronautPlatformVersion}"
    }
}
////////////////////////////////////////////////////////////////////////

version project.projectVersion
group "io.micronaut.starter"
ext {
    isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == "org.apache.groovy") {
                details.useVersion(groovyVersion)
            }
            if (details.requested.name == "spock-core") {
                details.useVersion(spockVersion)
            }
        }
    }
}

spotless {
    java {
        target 'src/main/java/**/*.java'
    }
}

dependencies {
    testImplementation("org.apache.groovy:groovy:$groovyVersion")
    testImplementation("org.spockframework:spock-core:$spockVersion") {
        exclude group: "org.apache.groovy", module: "groovy-all"
    }
}
